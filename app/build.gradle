/// MUST enable Google Cloud api : artifact registry, at Google Cloud Console
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "com.google.cloud.artifactregistry.gradle-plugin" version "2.1.1"
}

android {
    compileSdk 30

    defaultConfig {
        applicationId "com.example.run_flutter_module_in_android_app"
        minSdk 21
        targetSdk 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        /// MUST add as Guide indicated
        profile {
            initWith debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

repositories {
    google()
    mavenCentral()
    /// MUST include remote maven repository, details in Google Cloud console
    /// sample url : "artifactregistry://REGION-maven.pkg.dev/REPOSITORY-URL/REPOSITORY-ID"
    maven {
        url "artifactregistry://asia-southeast1-maven.pkg.dev/nct-flutter-module-mobile/release"
        /// REGION :asia-southeast1; REPOSITORY-URL : nct-flutter-module-mobile; REPOSITORY-ID : release
    }
    /// MUST add to include Flutter sdk in native platform
    maven {
        url 'https://storage.googleapis.com/download.flutter.io'
    }
}

dependencies {
    /// MUST define directly since `audio_service` library in `flutter module` included it,
    // otherwise, compile-time errors appear
    implementation "com.google.android.exoplayer:exoplayer-core:2.15.0"
    implementation "com.google.android.exoplayer:exoplayer-smoothstreaming:2.15.0"
    implementation "com.google.android.exoplayer:exoplayer-hls:2.15.0"
    implementation "com.google.android.exoplayer:exoplayer-dash:2.15.0"

    // SHOULD get *.aar from remote maven repository in here
    debugImplementation 'nct.mobile.flutter.module:flutter_debug:0.0.1'
    profileImplementation 'nct.mobile.flutter.module:flutter_profile:0.0.1'
    releaseImplementation 'nct.mobile.flutter.module:flutter_release:0.0.1'

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}